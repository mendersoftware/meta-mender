#@TYPE: Machine
#@NAME: Toradex Colibri iMX7D/S with Mender integration
#@SOC: i.MX 7Dual / i.MX 7Solo
#@DESCRIPTION: Machine configuration for Toradex Colibri iMX7 SOM with Mender integration

require conf/machine/colibri-imx7.conf

MACHINEOVERRIDES =. "colibri-imx7:"

# MTD partitioning defined for colibri_imx7:
#
MENDER_MTDIDS = "nand0=gpmi-nand"
MENDER_MTDPARTS = "gpmi-nand:512k(mx7-bcb),1536k(u-boot1)ro,1536k(u-boot2)ro,512k(u-boot-env),-(ubi)"

# u-boot-env partition holds 2 environments, each 128kB with env range set to 256kB
BOOTENV_SIZE = "0x20000"
BOOTENV_RANGE = "0x40000"

# 512MB of NAND flash
MENDER_STORAGE_TOTAL_SIZE_MB = "512"
# 16MB for data partition
MENDER_DATA_PART_SIZE_MB = "16"
# align to PEB size 128k
MENDER_FLASH_PEB_SIZE = "131072"
# Account for UBI overhead, see
# http://www.linux-mtd.infradead.org/doc/ubi.html#L_overhead for details,
MENDER_PARTITIONING_OVERHEAD_KB = "2048"
# all of above should give ~244MB for rootfs

# NOTE: this affects only u-boot's autoconfigured headers, the same variable is
# also used for generating fw_env.config but because of assumptions made in
# meta-mender-core/recipes-bsp/u-boot/u-boot-fw-utils-mender.inc we need ship
# our own config instead.
MENDER_UBOOT_ENV_STORAGE_DEVICE_OFFSET = "0x380000"
# calculated offsets are:
# - 0x380000
# - 0x3c0000 (redundant env)

# make sure that kernel and devicetree are installed
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "kernel-image kernel-devicetree"
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "mtd-utils mtd-utils-ubifs"

# pick u-boot-toradex-fsl-fw-utils as provider of fw_{printenv,setenv}
PREFERRED_PROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fsl-fw-utils"
PREFERRED_RPROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fsl-fw-utils"
