# Layer configuration for meta-mender
# Copyright 2017 Northern.tech AS

# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# Save layerdir for use in functions outside .conf scope
LAYERDIR_MENDER := "${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "mender"
BBFILE_PATTERN_mender = "^${LAYERDIR}/"
BBFILE_PRIORITY_mender = "6"

INHERIT += "mender-maybe-setup"

LAYERSERIES_COMPAT_mender = "scarthgap"
LAYERDEPENDS_mender = "core"

# See https://northerntech.atlassian.net/browse/MEN-3513 and
# https://northerntech.atlassian.net/browse/MEN-3781 and
# https://northerntech.atlassian.net/browse/MEN-3912.
#
# `has_journal` interferes with the checksum of the image when viewed from the block device. Oddly
# enough it is not present on disk, IOW, when the image is mounted readonly, the physical checksum
# on disk, and the checksum of the corresponding block device, are not the same. Work around this by
# disabling the journal for read-only filesystems, where it is useless anyway.
EXTRA_IMAGECMD:ext4:append = "${@bb.utils.contains('IMAGE_FEATURES', 'read-only-rootfs', ' -O ^has_journal', '', d)}"
