# Board specific layer configuration for meta-mender
# Copyright 2017 Northern.tech AS

# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "mender-qemu"
BBFILE_PATTERN_mender-qemu = "^${LAYERDIR}/"
BBFILE_PRIORITY_mender-qemu = "6"

LAYERSERIES_COMPAT_mender-qemu = "whinlatter"
LAYERDEPENDS_mender-qemu = "mender"

MENDER_EFI_LOADER:mender-image-uefi:qemux86 = "ovmf"
MENDER_EFI_LOADER:mender-image-uefi:qemux86-64 = "ovmf"

MENDER_STORAGE_DEVICE_DEFAULT:qemux86-64:mender-update-install = "/dev/sda"
MENDER_STORAGE_DEVICE_DEFAULT:qemux86:mender-update-install = "/dev/sda"

# To tell the difference from a vexpress-qemu image with only U-Boot.
MENDER_DEVICE_TYPE_DEFAULT:vexpress-qemu:mender-grub = "${MACHINE}-grub"
# To tell the difference from a qemux86-64 image with GRUB and UEFI.
MENDER_DEVICE_TYPE_DEFAULT:qemux86-64:mender-grub:mender-bios = "${MACHINE}-bios"
# To tell the difference from a qemux86-64 image with BIOS and MBR.
MENDER_DEVICE_TYPE_DEFAULT:qemux86-64:mender-grub:mender-bios:mender-image-gpt = "${MACHINE}-bios-grub-gpt"

# The rngd daemon included in this package constantly pegs the CPU at 100% for
# the first 3-7 minutes, and drastically reduces the performance in tests. We
# don't need it for most of our images, so just disable it. Our
# vexpress-qemu-flash image needs it though. For some reason the RNG source
# cannot be initialized there without it.
PACKAGE_EXCLUDE:append:mender-image = "${@' rng-tools' if d.getVar('MACHINE') != 'vexpress-qemu-flash' else ''}"

# Disable feature for compatibility with GRUB 2.04 (kirkstone)
# Recent versions of mke2fs enable the feature by default, but it renders the filesystem
# unrecognizable for older GRUB, blocking the kirkstone to scarthcap update path. See:
# * Bug report: http://savannah.gnu.org/bugs/?56897
# * Fix: https://git.savannah.gnu.org/cgit/grub.git/commit/?id=7fd5feff97c4b1f446f8fcf6d37aca0c64e7c763
# * mke2fs release notes: https://git.kernel.org/pub/scm/fs/ext2/e2fsprogs.git/tree/doc/RelNotes/v1.47.0.txt
EXTRA_IMAGECMD:ext4:append = " -O ^metadata_csum_seed"
